module reals where
import experiments/circle

def R-zero := elem Z-zero

def cis := R-rec S¹ (λ (_ : Z), base) (λ (_ : Z), loop)

def center-pos :=
N-ind (λ (n : N), Path R R-zero (elem (pos n))) (idp R-zero)
      (λ (n : N) (p : Path R R-zero (elem (pos n))), p ⬝ glue (pos n))

def center-neg :=
N-ind (λ (n : N), Path R R-zero (elem (neg n))) (glue (neg zero))⁻¹
      (λ (n : N) (p : Path R R-zero (elem (neg n))), p ⬝ (glue (neg (succ n)))⁻¹)

def center : Π (z : Z), Path R R-zero (elem z) :=
Z-ind (λ (z : Z), Path R R-zero (elem z)) center-pos center-neg

def vect (u v : Z) : Path R (elem u) (elem v) :=
(center u)⁻¹ ⬝ center v

def R-contr-glueᵀ (z : Z) :=
Path (Path R R-zero (elem (Z-succ z)))
  (subst R (λ (y : R), Path R R-zero y) (elem z) (elem (Z-succ z)) (glue z) (center z))
  (center (Z-succ z))

def R-contr : is-contr R :=
(elem Z-zero, R-ind (λ (y : R), Path R R-zero y) center
  (Z-ind R-contr-glueᵀ
    (N-ind (λ (n : N), R-contr-glueᵀ (pos n)) (idp (glue Z-zero))
      (λ (n : N) (p : R-contr-glueᵀ (pos n)), idp (center-pos (succ (succ n)))))
    (N-ind (λ (n : N), R-contr-glueᵀ (neg n)) (idp (idp R-zero))
      (λ (n : N) (p : R-contr-glueᵀ (neg n)), idp (center-neg n)))))

def dist : is-prop R := contr-impl-prop R R-contr

def Z-lift (f : Z → Z) : R → R :=
R-rec R (λ (z : Z), elem (f z)) (λ (z : Z), vect (f z) (f (Z-succ z)))

def Z-lift² (f : Z → Z → Z) : R → R → R :=
R-rec (R → R) (λ (z : Z), Z-lift (f z))
  (λ (z : Z), funext R (λ (_ : R), R) (Z-lift (f z)) (Z-lift (f (Z-succ z)))
    (λ (x : R), dist (Z-lift (f z) x) (Z-lift (f (Z-succ z)) x)))

def R-succ := Z-lift Z-succ
def R-neg  := Z-lift Z-neg′
def R-add  := Z-lift² Z-add

def reflect (A : U) (a b : A) : Id A a b → Path A a b :=
idJ A (λ (a b : A) (_ : Id A a b), Path A a b) a (idp a) b

def minus-1-ineq-0 (p : Path Z (neg zero) Z-zero) : ⊥ :=
coe (apd (λ (z : Z) (p : ∂ (neg zero) Z-zero z), Z-ind (λ (_ : Z), U) (λ (_ : N), ⊥) (λ (_ : N), N) z) p) zero

def 0-ineq-1 (p : Path Z Z-zero Z-one) : ⊥ :=
minus-1-ineq-0 (ap Z Z Z-pred Z-zero Z-one p)

def z-ineq-succ-zᵀ (z : Z) := Path Z z (Z-succ z) → ⊥

def z-ineq-succ-z : Π (z : Z), Path Z z (Z-succ z) → ⊥ :=
Z-ind z-ineq-succ-zᵀ
  (N-ind (λ (n : N), z-ineq-succ-zᵀ (pos n)) 0-ineq-1
    (λ (n : N) (f : z-ineq-succ-zᵀ (pos n)) (p : Path Z (pos (succ n)) (pos (succ (succ n)))),
       f (ap Z Z Z-pred (pos (succ n)) (pos (succ (succ n))) p)))
  (N-ind (λ (n : N), z-ineq-succ-zᵀ (neg n)) minus-1-ineq-0
    (λ (n : N) (f : z-ineq-succ-zᵀ (neg n)) (p : Path Z (neg (succ n)) (neg n)),
       f (ap Z Z Z-succ (neg (succ n)) (neg n) p)))

def z-strict-ineq-succ-z (z : Z) (p : Id Z z (Z-succ z)) : ⊥ :=
z-ineq-succ-z z (reflect Z z (Z-succ z) p)

def unglue (z : Z) (p : Id R (elem z) (elem (Z-succ z))) : ⊥ :=
z-strict-ineq-succ-z z (R-inj z (Z-succ z) p)

def cis-respects-unit : Id S¹ (cis R-zero) base := refl base

--def cis-respects-inverse : Π (x : R), Id S¹ (cis (R-neg x)) (ι (cis x)) := ?
--def cis-respects-add (x y : R) : Id S¹ (cis (R-add x y)) (μ (cis x) (cis y)) := ?