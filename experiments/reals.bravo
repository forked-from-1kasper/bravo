module reals where
import experiments/circle

def R-rec (A : U) := R-ind (λ (x : R), A)

def cis := R-rec S¹ (λ (_ : Z), base) (λ (_ : Z), loop)

def center : Π (z : Z), Path R (elem zero) (elem z) :=
Z-ind (λ (z : Z), Path R (elem zero) (elem z)) (idp (elem zero))
  (λ (z : Z) (p : Path R (elem zero) (elem z)), p ⬝ glue z)
  (λ (z : Z) (p : Path R (elem zero) (elem z)), p ⬝ (glue (pred z))⁻¹)

def vect (u v : Z) : Path R (elem u) (elem v) :=
(center u)⁻¹ ⬝ center v

def R-contr : is-contr R :=
(elem zero, R-ind (λ (y : R), Path R (elem zero) y) center
  (Z-ind (λ (z : Z),
    Path (Path R (elem zero) (elem (succ z)))
      (subst R (λ (y : R), Path R (elem zero) y)
        (elem z) (elem (succ z)) (glue z) (center z))
      (center (succ z)))
    (idp (glue zero))
    (λ (z : Z) (p : Path (Path R (elem zero) (elem (succ z)))
                         (center z ⬝ glue z) (center z ⬝ glue z)),
      idp (center z ⬝ glue z ⬝ glue (succ z)))
    (λ (z : Z) (p : Path (Path R (elem zero) (elem (succ z)))
                         (center z ⬝ glue z) (center z ⬝ glue z)),
      idp (center z))))

def dist : is-prop R := contr-impl-prop R R-contr

def Z-lift (f : Z → Z) : R → R :=
R-rec R (λ (z : Z), elem (f z)) (λ (z : Z), vect (f z) (f (succ z)))

def Z-con (A : U) (B : A → U) (f g : Π (x : A), B x)
  (p : Π (x : A), Path (B x) (f x) (g x)) (x : A) : Z → B x :=
Z-rec (B x) (f x) (λ (_ : B x), g x) (λ (_ : B x), f x)

def R-con (A : U) (B : A → U) (f g : Π (x : A), B x)
  (p : Π (x : A), Path (B x) (f x) (g x)) (x : A) : R → B x :=
R-rec (B x) (Z-con A B f g p x) (Z-ind (λ (z : Z), Path (B x) (Z-con A B f g p x z) (g x))
  (p x) (λ (z : Z) (_ : Path (B x) (Z-con A B f g p x z) (g x)), idp (g x))
  (λ (z : Z) (_ : Path (B x) (Z-con A B f g p x z) (g x)), p x))

def funext (A : U) (B : A → U) (f g : Π (x : A), B x)
  (p : Π (x : A), Path (B x) (f x) (g x)) :=
ap R (Π (x : A), B x) (λ (z : R) (x : A), R-con A B f g p x z)
     (elem zero) (elem (succ zero)) (glue zero)

def Z-lift² (f : Z → Z → Z) : R → R → R :=
R-rec (R → R) (λ (z : Z), Z-lift (f z))
  (λ (z : Z), funext R (λ (_ : R), R) (Z-lift (f z)) (Z-lift (f (succ z)))
    (λ (x : R), dist (Z-lift (f z) x) (Z-lift (f (succ z)) x)))

def R-add := Z-lift² Z-add

def reflect (A : U) (a b : A) : Id A a b → Path A a b :=
idJ A (λ (a b : A) (_ : Id A a b), Path A a b) a (idp a) b

def 0-ineq-1 (p : Path Z zero (succ zero)) : ⊥ :=
coe (cong (λ (z : Z) (p : ∂ zero (succ zero) z),
  Z-ind (λ (_ : Z), U) Z (λ (_ : Z) (_ : U), ⊥) (λ (_ : Z) (_ : U), ⊥) z) p) zero

def z-ineq-succ-z (z : Z) : Path Z z (succ z) → ⊥ :=
Z-ind (λ (z : Z), Path Z z (succ z) → ⊥) 0-ineq-1
  (λ (z : Z) (f : Path Z z (succ z) → ⊥) (p : Path Z (succ z) (succ (succ z))),
    f (ap Z Z pred (succ z) (succ (succ z)) p))
  (λ (z : Z) (f : Path Z z (succ z) → ⊥) (p : Path Z (pred z) z),
    f (ap Z Z succ (pred z) z p)) z

def z-strict-ineq-succ-z (z : Z) (p : Id Z z (succ z)) : ⊥ :=
z-ineq-succ-z z (reflect Z z (succ z) p)

axiom elem-inj (x y : Z) : Id R (elem x) (elem y) → Id Z x y

def unglue (z : Z) (p : Id R (elem z) (elem (succ z))) : ⊥ :=
z-strict-ineq-succ-z z (elem-inj z (succ z) p)

--def cis-over-add-elem (y : R) : Π (x : Z), Id S¹ (cis (R-add (elem x) y)) (cis y) :=
--Z-ind (λ (x : Z), Id S¹ (cis (R-add (elem x) y)) (cis y)) (refl (cis y)) ? ?

--def cis-over-add (x y : R) : Id S¹ (cis (R-add x y)) (μ (cis x) (cis y)) :=
--R-ind (λ (y : R), Id S¹ (cis (R-add x y)) (μ (cis x) (cis y)))
--  (λ (z : Z), refl (cis x)) ? y
